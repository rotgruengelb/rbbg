plugins {
	id 'fabric-loom' version '1.6-SNAPSHOT'
}

version = project.mod_version
group = project.maven_group

base {
	archivesName = project.archives_base_name
}

repositories {
	mavenCentral()
	maven { url "https://maven.rotgruengelb.net/releases" }
	maven { url "https://maven.terraformersmc.com/" }
	maven { url "https://maven.bawnorton.com/releases" }
	maven { url "https://maven.nucleoid.xyz/" }
	repositories { exclusiveContent { forRepository {
		maven { url "https://api.modrinth.com/maven" } }
		filter { includeGroup "maven.modrinth" } }
	}
}

dependencies {
	minecraft "com.mojang:minecraft:${project.minecraft_version}"
	mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
	modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

	include implementation("com.moulberry:mixinconstraints:1.0.1")
	include implementation(annotationProcessor("com.bawnorton.mixinsquared:mixinsquared-fabric:0.2.0-beta.6"))
	modImplementation annotationProcessor("net.rotgruengelb:nixienaut:${project.nixienaut_version}")
	modImplementation "com.terraformersmc:modmenu:${project.modmenu_version}"

	// modCompileOnly "maven.modrinth:physicsmod:2x0qld0N" // ~1.20
	modCompileOnly "maven.modrinth:physicsmod:9hVF9EMT" // ~1.21
	// modCompileOnly "maven.modrinth:stutterfix:mc1.20.3-0.2.0"
}

processResources {
	inputs.property "version", project.version
	inputs.property "loader_version", project.loader_version
	inputs.property "minecraft_version", project.minecraft_version
	inputs.property "nixienaut_version", project.nixienaut_version

	filesMatching("fabric.mod.json") {
		expand "version": project.version,
				"loader_version": project.loader_version,
				"minecraft_version": project.minecraft_version,
				"nixienaut_version": project.nixienaut_version
	}
}

loom {
	accessWidenerPath = file("src/main/resources/rbbg.accesswidener")
}

tasks.withType(JavaCompile).configureEach {
	it.options.release = 17
}

java {
	// withSourcesJar()
	sourceCompatibility = JavaVersion.VERSION_17
	targetCompatibility = JavaVersion.VERSION_17
}

jar {
	from("LICENSE") {
		rename { "${it}_${project.base.archivesName.get()}"}
	}
}